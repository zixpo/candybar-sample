// TODO: Remove `/*` and `*/` below to enable OneSignal
/*
buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.onesignal:onesignal-gradle-plugin:0.14.0'
    }
}
apply plugin: 'com.onesignal.androidsdk.onesignal-gradle-plugin'
*/
apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.CompileSdk

    namespace 'com.candybar.sample'

    defaultConfig {
        applicationId 'com.candybar.sample'
        minSdkVersion rootProject.ext.MinSdk
        targetSdkVersion rootProject.ext.TargetSdk
        versionCode 1
        versionName '1.0'

        // Naming app bundles
        setArchivesBaseName("${parent.name}-v${versionCode}")
    }

    bundle {
        language {
            enableSplit false
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        abortOnError false
    }
}

dependencies {
    /*
     * CandyBar Material Dashboard Library
     *
     * TODO: How to update the dashboard?!
     * Check this update guide for every version
     *      - https://github.com/zixpo/candybar-sample/wiki/Updating-the-dashboard
     * If you've missed some versions you've to also follow update guide for those versions
     */

    implementation 'com.github.zixpo:candybar:3.17.4'

    // TODO: Remove `//` below to enable OneSignal
    //implementation 'com.onesignal:OneSignal:[4.0.0, 4.99.99]'
}

// Code for automatically copying appfilter.xml and drawable.xml
// from `res/xml` directory to `assets` directory
task copyXMLs {
    ['appfilter.xml', 'drawable.xml'].each {file ->
        copy {
            from "$projectDir/src/main/res/xml/$file"
            into "$projectDir/src/main/assets/"
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleDebug' || task.name == 'assembleRelease') {
        task.dependsOn copyXMLs
    }
}